# Personal Zsh configuration file. It is strongly recommended to keep all
# shell customization and configuration (including exported environment
# variables such as PATH) in this file or in files sourced from it.
#
# Documentation: https://github.com/romkatv/zsh4humans/blob/v5/README.md.

# Periodic auto-update on Zsh startup: 'ask' or 'no'.
# You can manually run `z4h update` to update everything.
zstyle ':z4h:' auto-update 'no'
# Ask whether to auto-update this often; has no effect if auto-update is 'no'.
zstyle ':z4h:' auto-update-days '28'

# Start tmux if not already in tmux.
# zstyle ':z4h:' start-tmux       command tmux -u new -A -D -t z4h
zstyle ':z4h:' start-tmux no

# Move prompt to the bottom when zsh starts and on Ctrl+L.
zstyle ':z4h:' prompt-at-bottom 'yes'

# Keyboard type: 'mac' or 'pc'.
zstyle ':z4h:bindkey' keyboard 'mac'

# Mark up shell's output with semantic information.
zstyle ':z4h:' term-shell-integration 'yes'

# Right-arrow key accepts one character ('partial-accept') from
# command autosuggestions or the whole thing ('accept')?
zstyle ':z4h:autosuggestions' forward-char 'accept'

# Recursively traverse directories when TAB-completing files.
zstyle ':z4h:fzf-complete' recurse-dirs 'no'

# Enable direnv to automatically source .envrc files.
zstyle ':z4h:direnv' enable 'no'
# Show "loading" and "unloading" notifications from direnv.
zstyle ':z4h:direnv:success' notify 'yes'

# Enable ('yes') or disable ('no') automatic teleportation of z4h over
# SSH when connecting to these hosts.
# zstyle ':z4h:ssh:example-hostname1' enable 'yes'
# zstyle ':z4h:ssh:*.example-hostname2' enable 'no'
# The default value if none of the overrides above match the hostname.
zstyle ':z4h:ssh:*' enable 'no'

# Send these files over to the remote host when connecting over SSH to the
# enabled hosts.
# zstyle ':z4h:ssh:*' send-extra-files '~/.nanorc' '~/.env.zsh'

# Autoload functions.
autoload -Uz bashcompinit
autoload -Uz zmv
autoload -Uz edit-command-line
bashcompinit
zle -N edit-command-line

# Clone additional Git repositories from GitHub.
#
# This doesn't do anything apart from cloning the repository and keeping it
# up-to-date. Cloned files can be used after `z4h init`. This is just an
# example. If you don't plan to use Oh My Zsh, delete this line.
z4h install ohmyzsh/ohmyzsh || return
z4h install agkozak/zsh-z || return
z4h install hlissner/zsh-autopair || return

# Install or update core components (fzf, zsh-autosuggestions, etc.) and
# initialize Zsh. After this point console I/O is unavailable until Zsh
# is fully initialized. Everything that requires user interaction or can
# perform network I/O must be done above. Everything else is best done below.
z4h init || return

# Extend PATH.
path+=(
  $HOME/bin
  $HOME/.local/bin
)
fpath+=($XDG_CONFIG_HOME/zsh/functions)

# Export environment variables.
export GPG_TTY=$TTY
export EDITOR="nvim"
export VISUAL="nvim"
export LESSOPEN="|sh $XDG_CONFIG_HOME/less/lessfilter.sh %s"
export HOMEBREW_NO_AUTO_UPDATE=1
export HOMEBREW_BUNDLE_NO_LOCK=1
export HOMEBREW_BUNDLE_FILE="$XDG_CONFIG_HOME/homebrew/Brewfile"
export GOKU_EDN_CONFIG_FILE="$XDG_CONFIG_HOME/karabiner/karabiner.edn"
export DOCKER_HOST=$(docker context inspect | jq -r '.[0].Endpoints.docker.Host')

# Source plugins.
for file in $XDG_CONFIG_HOME/zsh/plugins/*; do z4h source $file; done
z4h source $(brew --prefix asdf)/asdf.sh
z4h source ohmyzsh/ohmyzsh/plugins/docker/docker.plugin.zsh
z4h source ohmyzsh/ohmyzsh/plugins/docker-compose/docker-compose.plugin.zsh
z4h source ohmyzsh/ohmyzsh/plugins/gitignore/gitignore.plugin.zsh
z4h source ohmyzsh/ohmyzsh/plugins/git/git.plugin.zsh
z4h source hlissner/zsh-autopair/autopair.zsh
z4h source $(pack completion --shell zsh)
_Z_CMD=j z4h load agkozak/zsh-z

path+=(
  $(go env GOPATH)/bin
)

# Define key bindings.
z4h bindkey edit-command-line Ctrl+V # edit the current command line

z4h bindkey undo Ctrl+/ Shift+Tab # undo the last command line change
z4h bindkey redo Option+/         # redo the last undone command line change

z4h bindkey z4h-cd-back Shift+Left     # cd into the previous directory
z4h bindkey z4h-cd-forward Shift+Right # cd into the next directory
z4h bindkey z4h-cd-up Shift+Up         # cd into the parent directory
z4h bindkey z4h-cd-down Shift+Down     # cd into a child directory

# Define functions and completions.
md() {
  [[ $# == 1 ]] && mkdir -p -- "$1" && cd -- "$1"
}

dotenv() {
  dotenv_file="${1:-.env}"
  [[ -f $dotenv_file ]] && set -a && source $dotenv_file && set +a
}

venv() {
  venv_path="${1:-.venv}"
  [[ -f $venv_path/bin/activate ]] && source $venv_path/bin/activate
}

brew-dump() {
  brew bundle dump --file=- | egrep "$(brew leaves | xargs printf '"%s"|')tap|cask" >$HOMEBREW_BUNDLE_FILE
}

# Define named directories: ~w <=> Windows home directory on WSL.
[[ -z $z4h_win_home ]] || hash -d w=$z4h_win_home

# Define aliases.
alias cat="bat"
alias ch="chezmoi"
alias chcd="cd $(chezmoi source-path)"
alias clear=z4h-clear-screen-soft-bottom
alias dk="docker"
alias ls="exa --all --group-directories-first"
alias la="$aliases[ls] -l --icons --git"
alias lt="$aliases[la] --tree --ignore-glob .git"
alias ldk="lazydocker"
alias lg="lazygit"
alias py="python"
alias pip="noglob pip"
alias poetry="noglob poetry"
alias p="$aliases[poetry]"
alias tf="terraform"
alias tree="tree -a -I .git"
alias vim="nvim"
alias y="yarn"
alias zshrc="$EDITOR ~/.zshrc"

# Set shell options: http://zsh.sourceforge.net/Doc/Release/Options.html.
setopt glob_dots    # no special treatment for file names with a leading dot
setopt no_auto_menu # require an extra TAB press to open the completion menu
